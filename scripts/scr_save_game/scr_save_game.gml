//Created 15/10/20
//Last updated 15/10/20

/*note of what needs to be saved:
-Main character you are currently
-Room
-Previous room
-Power level
-Maximum health
-Inventory
-Events
-The orb map
-The port map
-The barrier map
-The room array
Saving needs to just save a bunch of parameters - the item map, the inventory state, the room states, the events and player stats (health etc). Include room and last room.
*/

var _datamap=ds_map_create();

var savenumber=argument0;

//Save the player
ds_map_set(_datamap,	SAVE.PLAYER,		object_get_name(global.maincharacter));
//Save room
ds_map_set(_datamap,	SAVE.ROOM,			room_get_name(room));
//Previous room
ds_map_set(_datamap,	SAVE.PREVIOUSROOM,	room_get_name(controller_global.previous_room));
//Version
ds_map_set(_datamap,	SAVE.VERSION,		global.currentversion);
//NOTE: No longer save the name because I'm not going to use save game names anymore
//Power Level
ds_map_set(_datamap,	SAVE.POWERLEVEL,	global.playerpowerlevel);
//NOTE 2: No longer going to save health because you should just load in with full health
//Deaths
ds_map_set(_datamap,	SAVE.DEATHS,		global.deaths);			//Probably unused
//Inventory
ds_map_set(_datamap,	SAVE.INVENTORY,		ds_grid_write(global.inventory));
//Events - this function encodes all events as a string and returns it
ds_map_set(_datamap,	SAVE.EVENTS,		scr_events_to_list());
//Instance map
ds_map_set(_datamap,	SAVE.ITEMMAP,		ds_grid_write(global.instancegrid));
//room array
ds_map_set(_datamap,	SAVE.ROOMARRAY,		ds_grid_write(global.roomarray));
//To do list
ds_map_set(_datamap,	SAVE.TODO,			ds_grid_write(global.todo));
//Dialogue map
ds_map_set(_datamap,	SAVE.DIALOGUEMAP,	ds_map_write(global.dialoguemap));


var str=ds_map_write(_datamap);

//We also create a new little list containing icon, percentage completed and todays date (YYYY,MM,DD)
var _list=ds_list_create();
//Add icon
var icon=spr_edhead;
if (global.maincharacter==obj_Charlotte)
	icon=spr_charlottehead;
ds_list_add(_list,sprite_get_name(icon));
//Add date
var today=string(current_year)+"/"+string(current_month)+"/"+string(current_day);
ds_list_add(_list,today);
//Add percentage completed - we loop through global.roomarray for this
var grid=global.roomarray;
var height=ds_grid_height(grid);
var completedCount=0;
var totalCount=height*3;		//Num rooms, orbs and secrets found
//We get 1 point for each room all secrets are found, 1 point for each room discovered and 1 point for each room we collect all the orbs
//WE NEED TO FIND TOTAL SECRETS BEFORE DOING THIS
for (var i=0; i<height; i+=1)	{
	var orbsFound=ds_grid_get(grid,ROOMARRAY.ORBSFOUND,i);
	var secretsFound=ds_grid_get(grid,ROOMARRAY.SECRETSFOUND,i)
	var discovered=ds_grid_get(grid,ROOMARRAY.DISCOVERED,i);
	completedCount+=orbsFound+secretsFound+discovered;
}

completedCount-=global.percentagePointsAdjustment;
totalCount-=global.percentagePointsAdjustment;

var percent=completedCount/totalCount;
percent*=100;
ds_list_add(_list,percent);

var listStr=ds_list_write(_list);
ds_list_destroy(_list);

//Now write to the file
var file=file_text_open_write("save"+string(savenumber)+".OM");
file_text_write_string(file,"DO NOT EDIT THIS FILE");
file_text_writeln(file);
file_text_write_string(file,"DOING SO IS EXTREMELY LIKELY TO CORRUPT SAVE DATA AND CAUSE INSTABILITY");
file_text_writeln(file);
file_text_write_string(file,str);
file_text_writeln(file);
file_text_write_string(file,listStr);
file_text_close(file);

//Update save profile number
global.currentprofile=savenumber;

ds_map_destroy(_datamap);